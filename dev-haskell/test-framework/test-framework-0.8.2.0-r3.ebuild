# Copyright 1999-2021 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=8

# ebuild generated by hackport 0.5.5.9999

CABAL_FEATURES="lib profile haddock hoogle hscolour test-suite"
inherit haskell-cabal

DESCRIPTION="Framework for running and organising tests, with HUnit and QuickCheck support"
HOMEPAGE="http://haskell.github.io/test-framework/"

# The test suite exists on GitHub but not on Hackage
SRC_URI="https://github.com/haskell/${PN}/archive/refs/tags/core/v${PV}.tar.gz -> ${P}.tar.gz
	https://hackage.haskell.org/package/${P}/revision/8.cabal -> ${PF}.cabal"
S="${WORKDIR}/${PN}-core-v${PV}/core"
RESTRICT="mirror"

LICENSE="BSD"
SLOT="0/${PV}"
KEYWORDS="~amd64 ~x86"

RDEPEND=">=dev-haskell/ansi-terminal-0.4.0:=[profile?] <dev-haskell/ansi-terminal-0.12:=[profile?]
	>=dev-haskell/ansi-wl-pprint-0.5.1:=[profile?] <dev-haskell/ansi-wl-pprint-0.7:=[profile?]
	>=dev-haskell/base-orphans-0.1:=[profile?] <dev-haskell/base-orphans-0.9:=[profile?]
	>=dev-haskell/hostname-1.0:=[profile?] <dev-haskell/hostname-1.1:=[profile?]
	>=dev-haskell/old-locale-1.0:=[profile?] <dev-haskell/old-locale-1.1:=[profile?]
	>=dev-haskell/random-1.0:=[profile?] <dev-haskell/random-1.3:=[profile?]
	>=dev-haskell/regex-posix-0.72:=[profile?] <dev-haskell/regex-posix-0.97:=[profile?]
	>=dev-haskell/semigroups-0.18:=[profile?] <dev-haskell/semigroups-0.21:=[profile?]
	>=dev-haskell/xml-1.3.5:=[profile?] <dev-haskell/xml-1.4:=[profile?]
	>=dev-lang/ghc-7.4.1:=
"
DEPEND="${RDEPEND}
	>=dev-haskell/cabal-1.10
	test? ( >=dev-haskell/hunit-1.2
		>=dev-haskell/libxml-0.1.1
		>=dev-haskell/quickcheck-2.3 <dev-haskell/quickcheck-2.15 )
"

src_unpack() {
	unpack "${P}.tar.gz"
}

src_prepare() {
	# pull revised cabal from upstream
	cp "${DISTDIR}/${PF}.cabal" "${S}/${PN}.cabal" || die

	# Apply patches *after* pulling the revised cabal
	default
}
